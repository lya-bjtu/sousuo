/*******************************************************************************
 * BAREFOOT NETWORKS CONFIDENTIAL & PROPRIETARY
 *
 * Copyright (c) 2019-present Barefoot Networks, Inc.
 *
 * All Rights Reserved.
 *
 * NOTICE: All information contained herein is, and remains the property of
 * Barefoot Networks, Inc. and its suppliers, if any. The intellectual and
 * technical concepts contained herein are proprietary to Barefoot Networks, Inc.
 * and its suppliers and may be covered by U.S. and Foreign Patents, patents in
 * process, and are protected by trade secret or copyright law.  Dissemination of
 * this information or reproduction of this material is strictly forbidden unless
 * prior written permission is obtained from Barefoot Networks, Inc.
 *
 * No warranty, explicit or implicit is provided, unless granted under a written
 * agreement with Barefoot Networks, Inc.
 *
 ******************************************************************************/

#if __TARGET_TOFINO__ == 2
#include <t2na.p4>
#else
#include <tna.p4>
#endif

#include "common/headers.p4"
#include "common/util.p4"


/* A simple digest of a header fields and metadata. */
struct digest_a_t {
    mac_addr_t dst_addr;
    PortId_t port;
    mac_addr_t src_addr;
}

/* A second digest of header fields and metadata. */
struct digest_b_t {
    mac_addr_t dst_addr;
    bit<24> f1;
}

struct metadata_t {
    PortId_t port;
    bit<24>  metadata_to_learn;
}

// ---------------------------------------------------------------------------
// Ingress parser
// ---------------------------------------------------------------------------
parser SwitchIngressParser(
        packet_in pkt,
        out header_t hdr,
        out metadata_t ig_md,
        out ingress_intrinsic_metadata_t ig_intr_md) {

    TofinoIngressParser() tofino_parser;

    state start {
        tofino_parser.apply(pkt, ig_intr_md);
        transition parse_ethernet;
    }

    state parse_ethernet {
        pkt.extract(hdr.ethernet);
        transition select (hdr.ethernet.ether_type) {
            ETHERTYPE_IPV4 : parse_ipv4;
            default : reject;
        }
    }

    state parse_ipv4 {
        pkt.extract(hdr.ipv4);
        transition accept;
    }
}


// ---------------------------------------------------------------------------
// Ingress Deparser
// ---------------------------------------------------------------------------
control SwitchIngressDeparser(
        packet_out pkt,
        inout header_t hdr,
        in metadata_t ig_md,
        in ingress_intrinsic_metadata_for_deparser_t ig_dprsr_md) {
    Digest<digest_a_t>() digest_a;
    Digest<digest_b_t>() digest_b;

    apply {
        // Generate a digest, if digest_type is set in MAU.
        if (ig_dprsr_md.digest_type == 1) {
            digest_a.pack({hdr.ethernet.dst_addr, ig_md.port, hdr.ethernet.src_addr});
        } else if (ig_dprsr_md.digest_type == 2) {
            digest_b.pack({hdr.ethernet.dst_addr, ig_md.metadata_to_learn});
        }

        pkt.emit(hdr);
    }
}

control SwitchIngress(
        inout header_t hdr,
        inout metadata_t md,
        in ingress_intrinsic_metadata_t ig_intr_md,
        in ingress_intrinsic_metadata_from_parser_t ig_prsr_md,
        inout ingress_intrinsic_metadata_for_deparser_t ig_dprsr_md,
        inout ingress_intrinsic_metadata_for_tm_t ig_tm_md) {

    bool src_miss;
    PortId_t src_move;

    action smac_hit(PortId_t port) {
        src_move = port ^ ig_intr_md.ingress_port;
    }

    action smac_miss() {
        src_miss = true;
    }

    table smac {
        key = { hdr.ethernet.src_addr : exact; }
        actions = {
            smac_hit;
            smac_miss;
        }

        const default_action = smac_miss;
        size = 1024;
    }

    action dmac_hit(PortId_t port) {
        ig_tm_md.ucast_egress_port = port;
    }

    action dmac_hit_with_digest(PortId_t port, bit<24> f) {
        ig_tm_md.ucast_egress_port = port;
        md.metadata_to_learn = f;
        ig_dprsr_md.digest_type = 2;
    }

    table dmac {
        key = { hdr.ethernet.dst_addr : exact; }
        actions = {
            dmac_hit;
            dmac_hit_with_digest;
            NoAction;
        }

        const default_action = NoAction;
        size = 1024;
    }


    apply {
        // Keep a mutable copy of intrinsic metadata for digest.
        md.port = ig_intr_md.ingress_port;

        src_miss = false;
        src_move = 0;

        smac.apply();

        // Send a digest if MAC address is unknown or if it is known but 
        // attached to a different port.
        if (src_miss == true || src_move != 0) {
            ig_dprsr_md.digest_type = 1;
        }

        // Apply the DMAC table.  Note that if it runs the dmac_hit_with_digest
        // action it will override any digests that would've been generated by
        // the SMAC result processing.
        dmac.apply();

        // No need for egress processing, skip it and use empty controls for egress.
        ig_tm_md.bypass_egress = 1w1;
    }
}

Pipeline(SwitchIngressParser(),
         SwitchIngress(),
         SwitchIngressDeparser(),
         EmptyEgressParser(),
         EmptyEgress(),
         EmptyEgressDeparser()) pipe;

Switch(pipe) main;
